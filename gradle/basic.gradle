buildscript {
    repositories {
        maven { url "https://maven.aliyun.com/repository/gradle-plugin" }
        maven { url 'https://repo.spring.io/plugins-snapshot' }
        mavenCentral()
    }
    dependencies {
        classpath "com.diffplug.spotless:spotless-plugin-gradle:6.25.0"
        classpath "io.freefair.gradle:lombok-plugin:8.6"
        classpath "me.qoomon:gradle-git-versioning-plugin:6.4.2"
    }
}


apply plugin: "java-library"
apply plugin: "io.freefair.lombok"
apply plugin: "com.diffplug.spotless"
apply plugin: "me.qoomon.git-versioning"

gitVersioning.apply {
    version = rootProject.findProperty("version")
    refs {
        branch('master') {
            version = '${version}-${commit.short}'
        }
        branch('.+') {
            version = '${version}-${commit.short}-SNAPSHOT'
        }
    }
}

ext {
    junitVersion = "5.10.2"
    mockitoVersion = "5.11.0"
    guavaVersion = "33.0.0-jre"
    spotlessVersion = "1.21.0"
}

sourceCompatibility = 17
targetCompatibility = 17


dependencies {
    // utils
    implementation "com.google.guava:guava:${guavaVersion}"
    implementation "cn.hutool:hutool-core:${hutoolVersion}"


    // tests
    testImplementation "org.junit.jupiter:junit-jupiter-api:${junitVersion}"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${junitVersion}"
    testRuntimeOnly "org.mockito:mockito-junit-jupiter:${mockitoVersion}"
    testRuntimeOnly "org.mockito:mockito-core:${mockitoVersion}"

}

allprojects {

    apply plugin: "com.diffplug.spotless"
    apply plugin: "io.freefair.lombok"
    apply plugin: "me.qoomon.git-versioning"

    group "io.ghrc.heliannuuthus"


    repositories {
        maven { url "https://maven.aliyun.com/nexus/content/groups/public" }
        maven { url "https://maven.aliyun.com/nexus/content/repositories/jcenter" }
        maven {
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/heliannuuthus/${project.name}"
            credentials {
                username = rootProject.findProperty("gpr.user") ?: System.getenv("GPR_USER")
                password = rootProject.findProperty("gpr.token") ?: System.getenv("GPR_TOKEN")
            }
        }
        mavenCentral()
    }

    spotless {
        java {
            importOrder()
            removeUnusedImports()
            googleJavaFormat("${spotlessVersion}").groupArtifact("com.google.googlejavaformat:google-java-format")
            formatAnnotations()
        }
    }
}
