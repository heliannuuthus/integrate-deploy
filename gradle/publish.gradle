allprojects {
    apply plugin: "maven-publish"
    apply plugin: "java-library"
    group "io.github.heliannuuthus"
    version rootProject.findProperty("version")
    tasks.withType(Jar.class).configureEach {
        duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    }
    tasks.withType(GenerateModuleMetadata).configureEach {
        enabled = false
    }
    repositories {
        maven { url "https://maven.aliyun.com/repository/spring/" }
        maven { url "https://maven.aliyun.com/nexus/content/groups/public" }
        maven { url "https://maven.aliyun.com/nexus/content/repositories/jcenter" }
        maven {
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/heliannuuthus/${project.name}"
            credentials {
                username = rootProject.findProperty("gpr.user") ?: System.getenv("GPR_USER")
                password = rootProject.findProperty("gpr.token") ?: System.getenv("GPR_TOKEN")
            }
        }
        mavenCentral()
    }
}

subprojects {
    java {
        withSourcesJar()
        withJavadocJar()
    }

    jar {
        manifest {
            attributes(
                    "Manifest-Version": 1.0,
            )
        }
    }
    sourceSets.main {
        java.srcDirs("src/main/java")
    }

    publishing {
        repositories {
            maven {
                name "GitHubPackages"
                url "https://maven.pkg.github.com/heliannuuthus/${rootProject.name}"
                credentials {
                    username = rootProject.findProperty("gpr.user") ?: System.getenv("GPR_USER")
                    password = rootProject.findProperty("gpr.token") ?: System.getenv("GPR_TOKEN")
                }
            }
        }
        publications {
            mavenJava(MavenPublication) {
                from components.java
                artifactId = "${project.name}"
            }
        }
    }
}